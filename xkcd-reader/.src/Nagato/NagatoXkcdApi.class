' Gambas class file

Property Read Image As Image
Property Read Number As Integer
Property Read SafeTitle As String
Property Read Alt As String
Property Read H As Integer
Property Read W As Integer

Private Const ApiCurrent As String = "http://xkcd.com/info.0.json"
Private Const ApiSelect As String = "http://xkcd.com/&1/info.0.json"

Private $imageDownloader As New NagatoImageDownloader As "ImageDownloader"
'Private $xkcdDownloader As New NagatoXkcdDownloader As "XkcdDownloader"

Private $collection As Collection

Event Finished

Private Sub resetCollection(argApi As String)
    
    Dim yukiTemp As String = Temp()
    
    Exec ["wget", argApi, "-O", yukiTemp] Wait
    
    $collection = JSON.Decode(File.Load(yukiTemp))
    
    XkcdDownloader_Finished()
    
End

Public Sub GetCurrent()
    
    resetCollection(ApiCurrent)
    
End

Public Sub GetSelect(argNumber As Integer)
    
    resetCollection(Subst$(ApiSelect, argNumber))
    
End

Public Sub XkcdDownloader_Finished()
    
    $imageDownloader.Get($collection["img"])
    
End

Public Sub ImageDownloader_Finished()
    
    Raise Finished
    
End

Private Function Image_Read() As Image

    Return $imageDownloader.Image

End

Private Function Number_Read() As Integer

    Return $collection["num"]

End

Private Function SafeTitle_Read() As String

    Return $collection["safe_title"]

End

Private Function Alt_Read() As String

    Return Replace$($collection["alt"], ".", ".\n")

End

Private Function H_Read() As Integer

    Return $imageDownloader.H

End

Private Function W_Read() As Integer

    Return $imageDownloader.W

End
